import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        maven { url 'https://maven.minecraftforge.net' }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spongepowered.org/repository/maven-public" }
        mavenCentral()
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.1.+'
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'org.spongepowered.mixin'


version "1.0.5-beta"
group "gkappa.wrapfix"
archivesBaseName = "WrapFix"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'stable', version: '39-1.12'


    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {

            jvmArg "-Dfml.coreMods.load=gkappa.wrapfix.WrapFixLoadingPlugin"
            jvmArg "-Dmixin.hotSwap=true"
            jvmArg "-Dmixin.checks.interfaces=true"

            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {

            jvmArg "-Dfml.coreMods.load=gkappa.wrapfix.WrapFixLoadingPlugin"
            jvmArg "-Dmixin.hotSwap=true"
            jvmArg "-Dmixin.checks.interfaces=true"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

configurations {
    compileJar {
        implementation.extendsFrom(compileJar)
    }
}

repositories {
    maven { url "https://repo.spongepowered.org/maven" }
    maven {
        url "https://cfa2.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
        metadataSources {
            artifact()
        }
    }
    maven {
        url "https://maven.cleanroommc.com"
    }
    flatDir {
        dirs "libs"
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    annotationProcessor("org.spongepowered:mixin:0.8.5:processor")
    /*
    compileJar("org.spongepowered:mixin:0.8.5") {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }*/
    compileJar "com.ibm.icu:icu4j:73.2"

    runtimeOnly "zone.rong:mixinbooter:8.4"
    compileOnly "zone.rong:mixinbooter:8.4"

    compileOnly fg.deobf( "curse.maven:IndustrialWiresRE-521139:3474268" )
    compileOnly fg.deobf( "curse.maven:ImmersiveEngineering-231951:2974106" )
    compileOnly fg.deobf( "curse.maven:BetterQuesting-629629:4010084" )
    runtimeOnly "curse.maven:BetterQuesting-629629:4010084"
    compileOnly fg.deobf( "curse.maven:Botania-225643:3330934" )
    compileOnly fg.deobf( "curse.maven:Psi-241665:3085917" )
    compileOnly fg.deobf( "curse.maven:Baubles-227083:2518667" )


    //compileOnly ("blus:immersiveengineering:0.12-98")
    //compileOnly ("mantle:industrialwires:1.8-38")
    //compileOnly ("loli:mixinbooter:3.2")
    //compileOnly ("funguy:betterquesting:3.5.329")

    //runtimeOnly "curse.maven:MixinBooter-419286:3687785"
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "FMLCorePluginContainsFMLMod": "true",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "FMLCorePlugin": "gkappa.wrapfix.WrapFixLoadingPlugin",
                "ForceLoadAsMod": "true"
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

java {
    withSourcesJar()
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from (sourceSets.main.resources.srcDirs) {
        include("mcmod.info")
        expand("version": version)
    }
}

publishing {
  repositories {
    maven {
      name = "outlands"
      url = "https://maven.outlands.top/releases"
      credentials(PasswordCredentials)
      authentication {
        basic(BasicAuthentication)
      }
    }
  }
  publications {
    maven(MavenPublication) {
      groupId = "gkappa"
      artifactId = "wrapfix"
      version = "1.0.2-beta"
      from components.java
    }
  }
}

mixin {
    add sourceSets.main, "mixins.wrapfix.refmap.json"
    disableAnnotationProcessorCheck()
}

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.compileJar]
    relocate('com.ibm.icu','new.com.ibm.icu')
}

task thinShadowJar(type: ShadowJar) {
    archiveClassifier.set("thin")
    configurations = [project.configurations.compileJar]
    relocators = shadowJar.relocators

    dependencies {
        exclude(dependency("org.spongepowered:mixin"))
    }

    manifest {
        inheritFrom jar.manifest
    }

    from sourceSets.main.output
}

artifacts {
    archives shadowJar
    archives sourcesJar
    archives thinShadowJar
}

reobf {
    shadowJar {}
    thinShadowJar {}
}
